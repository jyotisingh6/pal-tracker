import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    // set a custom property
    ext {
        springBootVersion = '2.2.2.RELEASE'
    }
    // check for dependencies in Maven Central when resolving the
    // dependencies in the buildscript block
    repositories {
        mavenCentral()
    }
    // we need the spring boot plugin to run the build script
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
    dependencies {
        classpath 'mysql:mysql-connector-java:8.0.13'
    }
}
plugins {
    id "org.flywaydb.flyway" version "5.2.1"
}
// 2) apply some plugins
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
// 3) set some standard properties
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
// 4) repos to search to resolve dependencies for the project
repositories {
    mavenCentral()
}
// 5) project dependencies
dependencies {
    //implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.apache.commons:commons-lang3')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-jdbc')
    implementation ('mysql:mysql-connector-java')
    testImplementation('org.springframework.security:spring-security-test')
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}
test {
    useJUnitPlatform()
}
def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
bootRun.environment([
        "WELCOME_MESSAGE": "Hello from the review environment",
        "SPRING_DATASOURCE_URL": developmentDbUrl,
])
def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
test.environment([
        "WELCOME_MESSAGE": "Hello from test",
        "SPRING_DATASOURCE_URL": testDbUrl,
])

bootJar {
    archiveName="pal-tracker.jar"
}
flyway {
    url = developmentDbUrl
    user = "tracker"
    password = ""
    locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
    url = testDbUrl
}
